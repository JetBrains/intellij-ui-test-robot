buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'org.jetbrains.intellij'
    id 'org.jetbrains.kotlin.jvm'
    id 'java'
}

repositories {
    mavenCentral()
    maven { url = "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

def remoteRobotVersion = "0.11.7"

dependencies {
    testImplementation 'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion
    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // Logging Network Calls
    testImplementation 'com.squareup.okhttp3:logging-interceptor:4.9.2'

    // Video Recording
    implementation 'com.automation-remarks:video-recorder-junit5:2.0'
}


intellij {
    version.set('LATEST-EAP-SNAPSHOT')
}

downloadRobotServerPlugin {
    version.set(remoteRobotVersion)
}


runIdeForUiTests {
//    In case your Idea is launched on remote machine you can enable public port and enable encryption of JS calls
//    systemProperty "robot-server.host.public", "true"
//    systemProperty "robot.encryption.enabled", "true"
//    systemProperty "robot.encryption.password", "my super secret"

    systemProperty "robot-server.port", "8082"
    systemProperty "ide.mac.message.dialogs.as.sheets", "false"
    systemProperty "jb.privacy.policy.text", "<!--999.999-->"
    systemProperty "jb.consents.confirmation.enabled", "false"
    systemProperty "ide.mac.file.chooser.native", "false"
    systemProperty "apple.laf.useScreenMenuBar", "false"
    systemProperty "idea.trust.all.projects", "true"
    systemProperty "ide.show.tips.on.startup.default.value", "false"
//    systemProperty "eap.require.license", "true"
}

test {
    // enable here nad in runIdeForUiTests block - to log the retrofit HTTP calls
    // systemProperty "debug-retrofit", "enable"

    // enable encryption on test side when use remote machine
    // systemProperty "robot.encryption.password", "my super secret"
    useJUnitPlatform()
}
