buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id("org.jetbrains.intellij.platform")
    id 'org.jetbrains.kotlin.jvm'
    id 'java'
}

repositories {
    mavenCentral()
    maven { url = "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies" }

    intellijPlatform {
        defaultRepositories()
    }
}


def remoteRobotVersion = "0.11.23"

kotlin {
    jvmToolchain(17)
}

dependencies {
    testImplementation 'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion
    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'


    // Logging Network Calls
    testImplementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // Video Recording
    implementation 'com.automation-remarks:video-recorder-junit5:2.0'

    intellijPlatform {
        intellijIdeaCommunity("2024.1")
    }
}

intellijPlatformTesting.runIde {
    runIdeForUiTests {
        task {
            jvmArgumentProviders.add({
                [
                        "-Drobot-server.port=8082",
                        "-Dide.mac.message.dialogs.as.sheets=false",
                        "-Djb.privacy.policy.text=<!--999.999-->",
                        "-Djb.consents.confirmation.enabled=false",
                        "-Dide.mac.file.chooser.native=false",
                        "-DjbScreenMenuBar.enabled=false",
                        "-Dapple.laf.useScreenMenuBar=false",
                        "-Didea.trust.all.projects=true",
                        "-Dide.show.tips.on.startup.default.value=false",
                ]
            } as CommandLineArgumentProvider)
        }
        plugins {
            robotServerPlugin(remoteRobotVersion)
        }
    }
}

test {
    // enable here nad in runIdeForUiTests block - to log the retrofit HTTP calls
    // systemProperty "debug-retrofit", "enable"

    // enable encryption on test side when use remote machine
    // systemProperty "robot.encryption.password", "my super secret"
    useJUnitPlatform()
}
